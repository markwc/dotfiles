#################################
# .cshrc for regular users as of August 5, 2006.
# The master copy of this file is: /usr/local/sm/newuser/.cshrc

#################################
# Set some environmental variables:
#   Pre-defines USER/LOGNAME: SunOS4,SunOS5,Linux
#   Pre-defines LOGNAME only: HP-UX
if (! $?USER) setenv USER ""
if (! $?LOGNAME) setenv LOGNAME $USER
if ("$USER" == "") setenv USER $LOGNAME
setenv WHOAMI $LOGNAME
setenv HOSTNAME `hostname | sed "s/\..*//"`

#################################
# Specify editor:

# PLEASE MODIFY THE FOLLOWING TO BE ANY ONE OF: emacs, vi, gvim, nedit, etc.
setenv EDITOR "emacs"

#################################
# Specify default printer:
#
# Please see "/usr/local/info/printers" for the current list of printer
# names and locations and redefine your PRINTER variable accordingly.
# FOR EXAMPLE, to make c1-174 be your default printer use:
# if (! $?PRINTER) setenv PRINTER c1-174

# PLEASE MODIFY THE FOLLOWING LINE:
if (! $?PRINTER) setenv PRINTER c1-174

#################################
# Set following based on whether shell is interactive or non-interactive:
if ($?prompt) then
    # Set prompt for interactive shell:
    #   Note: if you want the full directory path displayed use `$cwd';
    #         if you want the tail component only use `$cwd:t'.
#    alias cd \
#	'set _old="$cwd"; chdir \!*; set prompt="\! $HOSTNAME":"$cwd% "'
#   alias precmd 'XTitle $cwd'
    cd "$cwd"	# Set the prompt via cd
    set prompt = "[$HOSTNAME] "
#
#    # Uncomment following to add current working ClearCase view to prompt:
    if (-e /usr/local/ccase/aliases/prompt) then
        source /usr/local/ccase/aliases/prompt
    endif
else
    # Don't define prompt for non-interactive shells, otherwise "cron" jobs,
    # "rsh", and "rcp" will fail due to "stty" and "tty" being executed!
endif

##################################
# Set Vendor/Arch/OS/Release/Derivative variable:
if (-x /usr/bin/VAORD) then
    set VAORD = (`/usr/bin/VAORD`)
else
    set VAORD = (SUN SUN4 SUNOS 5.8 SYSV R4)	# Assume Sun if VAORD unknown
endif
# Set OS variables:
set OS_SYS = `uname -s`
set OS_REL = (`uname -r | sed -e 's;[^0-9]; ;g'`)
if ($#OS_REL < 3) set OS_REL = ($OS_REL 0)
set OS_DER = (SYSV R4)
if ($OS_SYS == SunOS && $OS_REL[1] <= 4) set OS_DER = (BSD 4.2)
if ($OS_SYS == FreeBSD) set OS_DER = (BSD 4.4)

#################################
# Limit resources:
limit coredumpsize 0 >& /dev/null	# Prevent core dump files

#################################
# Set miscellaneous:
unset autologout	# Disable automatic logout feature
set cdpath = (~ ~/src)	# Extraneous path for cd
set history = 2000000	# Max number of saved commands
set notify		# Allow background tasks to notify user immediately
set noclobber		# Disallow overwriting using ">", require ">!" instead

umask 002		# Force new files to have protections rw-rw-r--
# NB: setting your umask to anything other than 002 will prevent you from
# working within a project-group with other users.

#################################
# File completion:
set filec		# Allow file completion using tab or escape key
set fignore = (.o)	# File extensions to ignore
set nobeep		# Disable beep during file completion
#set complete = enhance	# Ignore case during file completion
#unset addsuffix	# Prevent adding "/" during file completion

#################################
# TCSH-specific settings:
if ($?prompt && $?tcsh) then
    #bindkey -e		# Enable emacs-like bindings
    #bindkey -v		# Enable vi-like bindings
    bindkey '^r' i-search-back	# Incremental search backwards
    bindkey '^s' i-search-fwd	# Incremental search forwards
endif

#################################
# MS-Dos emulation commands:
alias cls clear
alias comp 'diff \!* | more'
alias copy 'cp -ip'
alias del 'rm -i'
alias dir 'ls -CF'
#alias print 'lpr'		# Print 1 page per sheet
alias print 'enscript -2r'	# Print 2 pages per sheet (conserves paper)
alias gprint 'enscript -2r -G'	# Print using gaudy-style page headers
alias ren 'mv -i'
alias type more

#################################
# Useful commands:
alias bdf '/usr/ucb/df'			# Berkeley df
alias bdfx '/usr/ucb/df -xmvfs'		# Berkeley df excluding ClearCase
alias bps '/usr/ucb/ps'			# Berkeley ps
alias cp 'cp -ip'			# preserve file dates during copies
alias dmp 'od -xc \!* | more'		# dump binary file
alias em emacs				# emacs editor
alias h 'history 30'			# most recent history
alias ka 'kermit -l /dev/ttya -b 9600'	# Kermit serial connection to port A
alias kb 'kermit -l /dev/ttyb -b 9600'	# Kermit serial connection to port B
alias kj 'kill -9 %\!*'			# Kill background-job number
alias lj jobs				# List background-job numbers
alias ldir '/bin/ls -laL \!* | grep "^d"' # List directories
alias lldir '/bin/ls -la \!* | grep "^d"' # List Local directories
alias ll '/bin/ls -la \!* | more'	# Long Listing
alias mv 'mv -i'			# confirm file overwrite
alias purge 'rm -i .~* .#* .*~ *~ *#;'	# Purge emacs backup files
alias rm 'rm -i'			# confirm file removal (\rm overrides)
alias stat '/bin/ps -ef | more'		# Process statistics
alias vd '/bin/ls -la \!* | more'	# Verbose Directory
alias vdt '/bin/ls -lat \!* | more'	# Verbose Directory sorted by Time
alias BS  "stty erase '^H'"		# Use BackSpace key for deleting
alias DEL "stty erase '^?'"		# Use Delete key for deleting

# Activate/Deactivate a program:
alias activate   'source /usr/local/activate/\!*'
alias deactivate 'source /usr/local/deactivate/\!*'

# Start a Remote XTerm on specified host (e.g.: "rxt pandora"):
alias rxt '(set rhost = $HOSTNAME; if ("\!*" != "") set rhost = "\!*"; rsh $rhost "setenv DISPLAY $DISPLAY;/usr/openwin/bin/xterm -T $USER@$rhost" &)>/dev/null'

# Start a Remote DTTerm on specified host (e.g.: "rdt pandora"):
alias rdt '(set rhost = $HOSTNAME; if ("\!*" != "") set rhost = "\!*"; rsh $rhost "setenv DISPLAY $DISPLAY;/usr/dt/bin/dtterm -title $USER@$rhost" &)>/dev/null'

#################################
# Path and directory commands:

# Put current directory into a variable for future reference:
alias putd 'set \!* = "$cwd"'

# Temporarily add a directory to your search path:
alias addpath 'set path = (\!* $path)'

# Temporarily add a directory to your man path:
alias addman 'setenv MANPATH \!*":"$MANPATH'

# Create back command to return to old directory:
alias back 'set _back="$_old"; set _old="$cwd"; cd "$_back"; unset _back; dirs'

#################################
# Substitute the following system dependent aliases:

if ($OS_DER[1] == BSD) then
    # Berkeley derivatives:
    alias bdf '/bin/df'
    alias bps '/bin/ps'
    alias dmp 'od -hav \!* | more'
    alias ldir 'ls -lagL \!* | grep "^d"'
    alias ll 'ls -lag \!* | more'
    alias stat 'ps -aux | more'
    alias vd 'ls -lag \!* | more'
    alias vdt 'ls -lagt \!* | more'
else if ($OS_DER[1] == SYSV) then
    # SystemV derivatives:
    alias ls '/bin/ls -F'
    # Alias SYS5 mail command to something usable:
    if (-x /usr/bin/mailx) alias mail '/usr/bin/mailx'
    if (-x /usr/ucb/mail)  alias mail '/usr/ucb/mail'
    # Alias SYS5 remote shell command to rsh:
    if (-x /usr/bin/resh)  alias rsh '/usr/bin/resh'
    if (-x /usr/bin/remsh) alias rsh '/usr/bin/remsh'
    # Exceptions:
    if ($OS_SYS == HP-UX) then
	unalias bdf
	alias df bdf
	unalias bps	# No equivalent
    else if ($OS_SYS == Linux) then
        alias ls '/bin/ls --color'
	alias bdf '/usr/local/bin/df'
	alias bdfx '/usr/local/bin/df -xmvfs'
	alias bps '/bin/ps'	# Supports both BSD/SysV syntax
	alias stat 'ps aux | more'
    endif
endif

#################################
# Source user's personal aliases (if any):

if (-e ~/.dotfiles/tcsh/aliases.tcsh) source ~/.dotfiles/tcsh/aliases.tcsh

#################################
# Source .login if entering a subshell via newgrp, cleartool, etc.:

if ($?shlvl) then
    if ($shlvl > 1) source ~/.login
endif

#################################
# RTI stuff

setenv NDDSHOME ~smpasi/RTI/ndds.5.0.0/
setenv RTI_LICENSE_FILE ~/RTI/rti_license.dat
alias rti $NDDSHOME/scripts/rtiddsgen

# rti -namespace -language C++ -replace -example x64Linux2.6gcc4.4.5 waveform.idl

#################################
# EOF.
